from datetime import date
def get_orthofinder_outdir():
    """
    Generate path to orthofinder gene trees folder with current date as written by orthofinder
    """
    today = date.today()
    monthDay = today.strftime("%b%d")
    outdir= f"results/orthofinder/Results_{monthDay}/Gene_Trees"
    return outdir

rule gunzip:
    """
    Decompress (if gzipped) protein files downloaded from public databases.
    """
    input:
        expand("resources/sequences/ensembl/{species}.pep.fasta.gz",species=ensembl_targets.loc[:,"species"]),
        expand("resources/sequences/ensemblgenomes/{species}.pep.fasta.gz",species=ensemblgenomes_targets.loc[:,"species"]),
        expand("resources/sequences/other/{species}.pep.fasta",species=other_targets.loc[:,"species"]),
        expand("resources/sequences/other_gz/{species}.pep.fasta.gz",species=other_gz_targets.loc[:,"species"]),
        expand("resources/sequences/gdrive/{species}.pep.fasta",species=gdrive_targets.loc[:,"species"]),
        reference_peptides="../ref_optimization/results/reference/Male_Med.ALL.Clustered.hq.CLEAN.cd_99.Min200.transdecoder_dir/longest_orfs.pep"
    output:
        expand("resources/sequences/{species}.pep.fasta", species=targets.index)
    params:
        copyfile=expand("resources/sequences/{species}.pep.fasta", species=config["species"])
    shell:
        """
        dir="resources/sequences"
        gzfiles=`find $dir/* -name '*.gz'`
        for file in $gzfiles; do
            gunzip $file
        done
        fastafiles=`find $dir/* -name '*.fasta'`
        for file in $fastafiles; do
            mv $file $dir
        done
        subdirs=`ls -d $dir/*/`
        rm -R $subdirs

        cp {input.reference_peptides} {params.copyfile}
        """

rule emapper_annotate:
    """
    Generate functional annotations using eggnog mapper.
    """
    input:
        expand("resources/sequences/{species}.pep.fasta", species=targets.index)
    output:
        "results/annotations/all.pep.emapper.annotations"
    params:
        outdir="results/annotations",
        script="workflow/scripts/annotate_emapper.py",
        peptides="resources/sequences/all.pep.fasta"
    conda:
        "../../workflow/envs/emapper.yaml" #eggnog-mapper=2.0.6, python=3.7.9
    shell:
        """
        cat resources/sequences/*.fasta > {params.peptides}
        python {params.script} {params.peptides} {params.outdir}
        """

rule orthofinder:
    """
    Infer gene trees from set of protein sequences downloaded from public databases.
    """
    input:
        expand("resources/sequences/{species}.pep.fasta", species=targets.index)
    output:
        directory(get_orthofinder_outdir())
    conda:
        "../../workflow/envs/orthofinder.yaml"
    threads: 20
    shell:
        """
        rm -rf results/orthofinder
        orthofinder -t {threads} -f resources/sequences -o results/orthofinder
        """

rule append_annotations:
    """
    Append functional annotations to sequence headers in gene trees.
    """
    input:
        # gene_trees_folder=directory(get_orthofinder_outdir()),
        annotations="results/annotations/all.pep.emapper.annotations",
        gene_trees_folder=expand("results/orthofinder/Results_{monthDay}/Gene_Trees",monthDay=ORTHODATE)
    output:
        "results/gene_trees/gene_trees.master.annotated.txt"
    params:
        script="workflow/scripts/append_annotations.py"
    resources:
        mem_mb=100000
    shell:
        """
        # Concatenate gene tree text files generated by orthofinder
        dir={input.gene_trees_folder}
        gene_trees=`find $dir/* -name '*.txt'`
        for file in $gene_trees; do
            (cat "$file"; echo) >> results/gene_trees/gene_trees.master.txt
        done

        # Run script to append annotations to protein IDs in gene trees
        python {params.script} {input.annotations} results/gene_trees/gene_trees.master.txt results/gene_trees
        """
